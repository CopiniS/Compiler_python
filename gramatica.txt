PROGRAMA            -->     INSTRUCOESINICIO

BLOCO               -->     NOVALINHA indent INSTRUCOES dedent
   
NOVALINHA           -->     novaLinha NOVALINHA2

NOVALINHA2          -->     novaLinha NOVALINHA2 | ε

NOVALINHA3          -->     NOVALINHA DEDENT

DEDENT              -->     dedent | ε

INSTRUCOESINICIO    -->     INSTRUCAO INSTRUCOES2 | ε  

INSTRUCOES          -->     INSTRUÇÃO INSTRUCOES2

INSTRUCOES2         -->     NOVALINHA3 INSTRUCAO INSTRUCOES2 | ε

INSTRUCAO           -->     INSTRUCAOSIMPLES | INSTRUCAOCOMPOSTA

INSTRUCAOSIMPLES    -->     LEITURA
                    |       ESCRITA
                    |       ATRIBUIÇÃO
                    |       RETORNO
                    |       passa

INSTRUCAOCOMPOSTA   -->     CONDICIONAL
                    |       FUNÇÃO 
                    |       ENQUANTO 
                    |       CLASSE
                    |       PARA

CONDICIONAL         -->     se ( EXPRESSAO ) : BLOCO SENAOSE SENAO

SENAOSE             -->     senaose ( EXPRESSAO ) : BLOCO | ε

SENAO               -->     senao : BLOCO | ε 

FUNÇÃO              -->     funcao id ( PARAMETRO ) : BLOCO

PARAMETRO           -->     id PARAMETRO2   |  ε

PARAMETRO2          -->     , id PARAMETRO2 |  ε

ENQUANTO            -->     enquanto ( EXPRESSAO ) : BLOCO

CLASSE              -->     classe id : BLOCO

LEITURA             -->     ler ( TERMOLEITURA NOVOTERMOLEITURA )

TERMOLEITURA        -->     id DIMENSAO

NOVOTERMOLEITURA    -->     , TERMOLEITURA NOVOTERMOLEITURA | ε

DIMENSAO            -->     [ EXPR_ADITIVA ] DIMENSAO | ε

ESCRITA             -->     escrever ( TERMOESCRITA NOVOTERMOESCRITA ) 

TERMOESCRITA        -->     id DIMENSAO | CONSTANTE | texto

NOVOTERMOESCRITA    -->     , TERMOESCRITA NOVOTERMOESCRITA | ε

ATRIBUIÇÃO          -->     id = COMPLEMENTO 

COMPLEMENTO         -->     EXPRESSAO | FUNÇÃO

RETORNO             -->     retorna EXPRESSAO

EXPRESSAO           -->     EXPR_OU

EXPR_OU             -->     EXPR_E EXPR_OU2

EXPR_OU2            -->     ou EXPR_E EXPR_OU2 | ε

EXPR_E              -->     EXPR_RELACIONAL EXPR_E2

EXPR_E2             -->     e EXPR_RELACIONAL EXPR_E2 | ε

EXPR_RELACIONAL     -->     EXPR_ADITIVA EXPR_RELACIONAL2

EXPR_RELACIONAL2    -->     OP_COMPARATIVO EXPR_ADITIVA | ε

OP_COMPARATIVO      -->     < | <= | > | >= | == | != 

EXPR_ADITIVA        -->     EXPR_MULTIPLICATIVA EXPR_ADITIVA2

EXPR_ADITIVA2       -->     OP_ADITIVO EXPR_MULTIPLICATIVA EXPR_ADITIVA2 | ε

OP_ADITIVO          -->     + | -

EXPR_MULTIPLICATIVA -->     FATOR EXPR_MULTIPLICATIVA2

EXPR_MULTIPLICATIVA2-->     OP_MULTIPLICATIVO FATOR EXPR_MULTIPLICATIVA2 | ε

OP_MULTIPLICATIVO   -->      * | / 

FATOR               -->     SINAL TERMO | texto | ! FATOR | ( EXPRESSAO )

TERMO               -->     id DIMENSAO2 | CONSTANTE

DIMENSAO2           -->     [ EXPR_ADITIVA ] DIMENSAO2 | ε

SINAL               -->     + | - | ε

CONSTANTE           -->     num_int | num_dec
